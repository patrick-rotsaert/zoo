#
# Copyright (C) 2024 Patrick Rotsaert
# Distributed under the Boost Software License, Version 1.0.
# (See accompanying file LICENSE or copy at
# http://www.boost.org/LICENSE_1_0.txt)
#

cmake_minimum_required(VERSION 3.19)

if(IS_DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/../modules)
	list(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR}/../modules)
endif()

include(CMakeFindDependencyMacro)

foreach(dependency @PROJECT_DEPENDENCIES@)
	string(REPLACE "," ";" dependency "${dependency}")
	find_dependency(${dependency})
endforeach()

set(_zoo_supported_components @zoo_FIND_PACKAGE_COMPONENTS@)
set(_zoo_requested_components ${${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS})
if(_zoo_requested_components)
	list(PREPEND _zoo_requested_components common)
	list(REMOVE_DUPLICATES _zoo_requested_components)
else()
	set(_zoo_requested_components ${_zoo_supported_components})
endif()

foreach(_comp IN LISTS _zoo_requested_components)
	if (NOT _comp IN_LIST _zoo_supported_components)
		set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "zoo does not have a component named '${comp}'.")
		set(${CMAKE_FIND_PACKAGE_NAME}_FOUND False)
		return()
	endif()
	set(_zoo_shared_targets "${CMAKE_CURRENT_LIST_DIR}/zoo-${_comp}-shared-targets.cmake")
	set(_zoo_static_targets "${CMAKE_CURRENT_LIST_DIR}/zoo-${_comp}-static-targets.cmake")
	if(DEFINED zoo_SHARED_LIBS AND zoo_SHARED_LIBS)
		set(_zoo_linkage shared)
	elseif(DEFINED zoo_SHARED_LIBS AND NOT zoo_SHARED_LIBS)
		set(_zoo_linkage static)
	elseif(BUILD_SHARED_LIBS)
		if(EXISTS "${_zoo_shared_targets}")
			set(_zoo_linkage shared)
		else()
			set(_zoo_linkage static)
		endif()
	else()
		if(EXISTS "${_zoo_static_targets}")
			set(_zoo_linkage static)
		else()
			set(_zoo_linkage shared)
		endif()
	endif()
	set(_zoo_targets "${_zoo_${_zoo_linkage}_targets}")
	if(NOT EXISTS "${_zoo_targets}")
		set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "zoo component ${_comp} '${_zoo_linkage}' libraries were requested but not found.")
		set(${CMAKE_FIND_PACKAGE_NAME}_FOUND False)
		return()
	endif()
	include("${_zoo_targets}")
endforeach()
