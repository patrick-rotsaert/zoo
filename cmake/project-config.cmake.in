#
# Copyright (C) 2024 Patrick Rotsaert
# Distributed under the Boost Software License, Version 1.0.
# (See accompanying file LICENSE or copy at
# http://www.boost.org/LICENSE_1_0.txt)
#

cmake_minimum_required(VERSION 3.19)

include(CMakeFindDependencyMacro)

foreach(dependency @PROJECT_DEPENDENCIES@)
	string(REPLACE "," ";" dependency "${dependency}")
	find_dependency(${dependency})
endforeach()

set(zoo_known_comps static shared)
set(zoo_comp_static NO)
set(zoo_comp_shared NO)

foreach(comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
	if(comp IN_LIST zoo_known_comps)
		set(zoo_comp_${comp} YES)
	else()
		set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "zoo does not have a component '${comp}'.")
		set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
		return()
	endif()
endforeach()

if(zoo_comp_static AND zoo_comp_shared)
	set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "zoo 'static' and 'shared' components are mutually exclusive.")
	set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
	return()
endif()

set(zoo_static_targets "${CMAKE_CURRENT_LIST_DIR}/zoo-static-targets.cmake")
set(zoo_shared_targets "${CMAKE_CURRENT_LIST_DIR}/zoo-shared-targets.cmake")

macro(zoo_load_targets type)
	if(NOT EXISTS "${zoo_${type}_targets}")
		set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE "zoo '${type}' libraries were requested but not found.")
		set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
		return()
	endif()
	include("${zoo_${type}_targets}")
endmacro()

if(zoo_comp_static)
	zoo_load_targets(static)
elseif(zoo_comp_shared)
	zoo_load_targets(shared)
elseif(DEFINED zoo_SHARED_LIBS AND zoo_SHARED_LIBS)
	zoo_load_targets(shared)
elseif(DEFINED zoo_SHARED_LIBS AND NOT zoo_SHARED_LIBS)
	zoo_load_targets(static)
elseif(BUILD_SHARED_LIBS)
	if(EXISTS "${zoo_shared_targets}")
		zoo_load_targets(shared)
	else()
		zoo_load_targets(static)
	endif()
else()
	if(EXISTS "${zoo_static_targets}")
		zoo_load_targets(static)
	else()
		zoo_load_targets(shared)
	endif()
endif()
